# Nombres de Fibonacci pairs

Chaque terme dans la suite de Fibonacci est généré en additionnantles  deux termes qui le précèdent.
En commencant avec 1 et 2, les 10 premiers termes sont : 1, 2, 3, 5, 8, 13, 21, 34, 55, 89.

En considérent les termes de la suite de Fibonacci dont les valeur n'excèdent pas 4 000 000, trouver la somme des termes dont la valeur est paire.

## Définition de la suite

On peut définir la suite de différentes façons.

### Définition naïve

La définition habituelle est très simple et utilise la récursion :

```haskell
fib :: Integer -> Integer
```
Cela indique le type de la fonction (elle retourne un entier quand on lui applique un entier).
```haskell
fib 0 = 1
fib 1 = 1
fib n = fib (n - 1) + fib (n - 2)
```
La fonction s'appelle elle-même, on appelle ça la récursion.

### Définition efficace

Cette définition est compliquée à comprendre pour ceux qui ne sont pas familiers avec Haskell, mais je vais tenter de l'expliquer.
```haskell
fibs :: [Integer]
```
On peut remarquer que définie ainsi, la suite est une liste.
```haskell
fibs = 1 : 1 : zipWith (+) fibs (tail fibs)
```
La fonction `:` sert à concaténer un élément à une liste. Par exemple : `3 : [5, 2, 1]` est égal à `[3, 5, 2, 1]`.
D'ailleurs, `[3, 5, 2, 1]` est le *sucre syntaxique* pour `3:5:2:1:[]`, car c'est de la dernière façon que Haskell représente les listes.  
On sait donc que la liste commence par `[0, 1, ...]`, ce qui est logique pour la suite de Fibonacci.

Ensuite, expliquons la fonction `zipWith :: (a -> b -> c) -> [a] -> [b] -> [c]`.
Son type nous indique qu'elle prend comme argument une fonction qui prend comme arguments un a et un b, et qui renvoie un c, ainsi qu'une liste de a, une liste de b, et qu'elle renvoie une liste de c.
Les lettres a, b et c représentent de types inconnus.  
La fonction `zipWith` va appliquer à la fonction passée en argument le premier élément de la liste de a, ainsi que le premier élément de la liste de b, et va ranger le c retourné par la fonction dans une liste, et va faire de même avec tous les éléments des listes.
Un exemple concret :
```haskell ignore
>>> zipWith (+) [1, 2, 3] [4, 5, 6, 7]
[5,7,9]
```
1 et 4 sont appliqués à la fonction `+` qui renvoie un 5, puis 2 et 5, ce qui renvoie un 7, et ainsi de suite.
La fonction `zipWith` s'arrête quand elle est arrivée au bout d'une des deux listes.  
Dans notre cas, la fonction `zipWith` a pour arguments la fonction `+`, la liste `fibs` (et oui, de la récursion), et la liste créée par `tail fibs`.

La fonction `tail` renvoie la liste sans le premier élément, comme ceci :
```haskell ignore
>>> tail [3, 5, 2, 1]
[5, 2, 1]
```

Graphiquement, la liste se créé comme ceci :
```
1 1   1 1 2 3 5  8  13
    + 1 2 3 5 8  13 21
= 1 1 2 3 5 8 13 21 34
```

## Filtrage

Comme pour le premier problème, on peut utiliser la fonction `filter` :

```haskell
m1 = sum . takeWhile (<= 4000000) . filter even $ fibs
```

Ou une comprehension de liste :

```haskell
m2 = sum . takeWhile (<= 4000000) $ [x | x <- fibs, even x]
```

## Méthode efficace

Mais comme pour le problème précédent, il y a une méthode plus efficace si on y réfléchit un peu !

On peut remarquer que le second élément de la suite est pair, ainsi que le 5ième, le 8ième, le 11ième, et ainsi de suite.

Soit `F(n)` la fonction qui renvoie le nième terme de la suite de Fibonacci.  
`F(0) = 1` ; `F(1) = 1` ; `F(n) = F(n-1) + F(n-2)`  
Prouvons que seulement `F(2 + 3p)` est pair pour tout entier naturel p.

* Initialisation : `F(0)` et `F(1)` sont impairs, `F(2) = F(0) + F(1) = 2` est pair.

* Hérédité : Supposons que `F(3p)` et `F(1 + 3p)` sont impairs, et que `F(2 + 3p)` est pair.
```
F(3 + 3p) = F(3(p+1)) = F(1 + 3p) + F(2 + 3p) = impair + pair = impair
F(1 + 3(p+1)) = F(2 + 3p) + F(3(p+1)) = pair + impair = impair
F(2 + 3(p+1)) = F(3(p+1)) + F(1 + 3(p+1)) = impair + impair = pair
```
La propriété est donc vraie pour le rang p+1

* Conclusion : Seulement `F(2 + 3p)` est pair pour tout entier naturel p.

On peut alors tenter de créer une nouvelle suite G telle que `G(n) = F(2 + 3n)`.
```
G(0) = F(2) = 2
G(1) = F(5) = 8
G(n) = F(2 + 3n)
     = F(1 + 3n) + F(3n)
     = F(3n) + F(2 + 3(n-1)) + F(2 + 3(n-1)) + F(1 + 3(n-1))
     = 2 G(n-1) + F(2 + 3(n-1)) + F(1 + 3(n-1)) + F(3(n-1)) + F(2 + 3(n-2))
     = 4 G(n-1) + G(n-2)
```

On peut maintenant faire la définition naïve :
```haskell
efib 0 = 2
efib 1 = 8
efib n = 4 * efib (n - 1) + efib (n - 2)
```

Ou la définition efficace :
```haskell
efibs = 2 : 8 : zipWith (\x y -> 4 * x + y) (tail efibs) efibs
```

Et maintenant il suffit de faire :
```haskell
m3 = sum . takeWhile (<= 4000000) $ efibs
```

---

```haskell
main = putStrLn $ show m3
```
